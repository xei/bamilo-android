buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'newrelic'
apply plugin: 'usea4s'
apply plugin: 'pmd'
apply plugin: 'findbugs'

/**
 * Check if BUILD_VERSION_NUMBER environment variable is defined and set it as version code.
 * This var is defined when the apk is being created by jenkins.
 *
 * @return The app versionCode: 1 when app is created locally or current jenkins version number.
 */
def getAppVersionCode(int releaseVersionCode) {
    // CASE DEV: is used a system var
    if (System.getenv('BUILD_VERSION_NUMBER') != null) {
        releaseVersionCode = Integer.valueOf(System.getenv('BUILD_VERSION_NUMBER'))
    }
    return releaseVersionCode
}

/**
 * PROJECT
 */
android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        applicationId "com.bamilo.android"
        versionName "2.4.1"
        versionCode getAppVersionCode(36)
        resConfigs "fa"
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4G"
    }

    signingConfigs {
        release {
            storeFile file("keystores/bamilo_android.keystore")
            storePassword "bamiloandroid"
            keyAlias "bamilo_android"
            keyPassword "bamiloandroid"
        }

    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    buildTypes {
        debug {
            debuggable true
            // # Building Apps with Over 65K Methods
            // 1. Using multi dex support
            multiDexEnabled true
            // 2. Using proguard to reduce number of methods
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project-pro.txt', 'proguard-project-debug.txt', 'proguard-project-dev.txt'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project-pro.txt', 'proguard-project-market.txt'
        }
    }
    productFlavors {
        live {

        }
        staging {

        }
    }

    lintOptions {
        disable 'RtlSymmetry', 'IconLocation', 'IconDipSize', 'IconXmlAndPng', 'RtlHardcoded'
        abortOnError false
        disable 'ExtraTranslation', 'MissingTranslation'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileNameRemoveReferences = outputFile.name.replace('JumiaApp', "App")
                output.outputFile = new File(outputFile.parent, fileNameRemoveReferences)
            }
        }
    }
}

/**
 * List of debug tools used only for debuggable apps.
 */
List debugToolsDependencies = [
        // #MULTIDEX
        'com.android.support:multidex:1.0.1',
        // #STETHO :: http://facebook.github.io/stetho/
        'com.facebook.stetho:stetho:1.3.1',
        'com.facebook.stetho:stetho-okhttp:1.3.1',
        // #LEAK :: https://github.com/square/leakcanary
        'com.squareup.leakcanary:leakcanary-android:1.4-beta2',
        // #SCAPEL :: https://github.com/JakeWharton/scalpel
        'com.jakewharton.scalpel:scalpel:1.1.2',
        // #DEBUG DRAWER :: https://github.com/palaima/DebugDrawer
        'io.palaima.debugdrawer:debugdrawer:0.7.0',
        'io.palaima.debugdrawer:debugdrawer-base:0.7.0',
        'io.palaima.debugdrawer:debugdrawer-commons:0.7.0',
        'io.palaima.debugdrawer:debugdrawer-scalpel:0.7.0',
]


dependencies {
    /** ############### LOCAL LIBS ############### **/
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libraries:com.mobile.framework')
    /** ############### DEBUG TOOLS ############### **/
    debugCompile debugToolsDependencies
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.6@aar') {
        transitive = true;
    }

    compile 'com.android.support:support-v4:23.4.0'
}

/**
 * All flavours and build types
 */
android.applicationVariants.all { variant ->
    println "****************************"
    println "variant: ${variant.name}"
    println "flavor: ${variant.flavorName}"
    println "productFlavors: ${variant.productFlavors[0].name}"
    println "buildType: ${variant.buildType.name}"
    println "applicationId: ${variant.applicationId}"
    println "versionName: ${variant.versionName}"
    println "versionCode: ${variant.versionCode}"
    println "****************************"
}

/**
 * Validate all translations
 */
task translationsValidation << {
    executeWaitAndPrintCmd(['ruby', 'JumiaApp/scripts/validate_str_placeholders.rb'])
}

/**
 *  PMD static analysis
 */
task pmd(type: Pmd) {
    ignoreFailures = true
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    //ruleSets = ["java-basic", "java-strings", "java-braces"]
    ruleSetFiles = files("pmd-ruleset.xml")
    ruleSets = []
}

/**
 * Findbugs static analysis
 */
task findbugs(type: FindBugs) {
    ignoreFailures = true
    classes = fileTree('build/intermediates/classes/')
    source = fileTree('src/main/java/')
    classpath = files()
    effort = 'max'
//    shows all warnings: low, medium, high. By default is set to medium and high
//    reportLevel = "low"
    excludeFilter = file("findbugs-exclude.xml")
    reports {
        xml {
            destination "build/reports/findbugs/findbugs.xml"
        }
    }
}

/**
 * Execute cmd command printing it in console and also the result of the command.
 * @param cmd A shell command
 * @return
 */
def executeWaitAndPrintCmd(List cmd) {
    println cmd
    def proc = cmd.execute()
    proc.waitFor()
    println proc.text
}

/**
 * Calabash tests
 */
task testCalabash << {
    //testCalabash -Pcountry=NG
    //calabash-android run /home/mobile/rocket/jenkins/workspace/Jumia_Calabash_2.2_NG/AutomatedTests/pt.rocket.jumia.dev/bin/JumiaDev-debug.apk --t @Calabash_Tests country=${COUNTRY} -f json -o cucumber.json -f html -o test.html -f pretty
    executeWaitAndPrintCmd(['calabash-android', 'run', './JumiaApp/build/outputs/apk/JumiaApp-jumia-debug.apk', '--t', '@Calabash_Tests', 'country=$country'])
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}
