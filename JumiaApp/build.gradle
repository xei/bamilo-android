apply plugin: 'com.android.application'
apply plugin: 'newrelic'
apply plugin: 'pmd'
apply plugin: 'findbugs'


/**
 * Check if BUILD_VERSION_NUMBER environment variable is defined and set it as version code.
 * This var is defined when the apk is being created by jenkins.
 *
 * @return The app versionCode: 1 when app is created locally or current jenkins version number.
 */
def getAppVersionCode() {

    def versionCode = 1
    if (System.getenv('BUILD_VERSION_NUMBER') != null) {
        versionCode = Integer.valueOf(System.getenv('BUILD_VERSION_NUMBER'))
    }

    return versionCode
}
/**
 *
 * if version name is defined on flavour the jenkins will use that name when creating the apk on the CI server or locally.
 *
 * if version name environment variable is setted on CI server and not on the flavour, the apk locally will be build with the versionName value,
 *and on the CI server with the version name setted on the BUILD_VERSION_NAME environment variable.
 *
 * if the version name isn't setted anywhere the apk will be build both locally and on the CI server with version name setted on versionName variable.
 *
 * @return the version name
 */
def getAppVersionName() {

    def versionName = 2.4
    if (System.getenv('BUILD_VERSION_NAME') != null) {
        versionName = System.getenv('BUILD_VERSION_NAME')
    }

    return versionName
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.mobile.view"
        minSdkVersion 9
        versionCode = getAppVersionCode()
        versionName = getAppVersionName()

        resConfigs "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
    }

    signingConfigs {
        releaseJumiaSigning {
            storeFile file("jumia.keystore")
            storePassword "jumia123"
            keyAlias "jumia"
            keyPassword "jumia123"
        }

        releaseDarazSigning {
            storeFile file("src/daraz/daraz_android.keystore")
            storePassword "darazandroid"
            keyAlias "daraz_android"
            keyPassword "darazandroid"
        }

        releaseShopSigning {
            storeFile file("src/shop/shop_android.keystore")
            storePassword "shopandroid"
            keyAlias "shop_android"
            keyPassword "shopandroid"
        }

        releaseBamiloSigning {
            storeFile file("src/bamilo/bamilo_android.keystore")
            storePassword "bamiloandroid"
            keyAlias "bamilo_android"
            keyPassword "bamiloandroid"
        }

        releaseBlackberrySigning {
            storeFile file("src/blackberry/jumia_blackberry.keystore")
            storePassword "jumiablackberry"
            keyAlias "jumia_blackberry"
            keyPassword "jumiablackberry"
        }

        releaseJumiaMTNSigning {
            storeFile file("src/jumiaMTNNg/jumia_mtnng.keystore")
            storePassword "jumiamtnng"
            keyAlias "jumia_mtnng"
            keyPassword "jumiamtnng"
        }

        releaseJumiaNokiaXSigning {
            storeFile file("src/jumiaNokiaX/jumia_nokiax.keystore")
            storePassword "jumianokiax"
            keyAlias "jumia_nokiax"
            keyPassword "jumianokiax"
        }

    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    /**
     * proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project-market.txt'
     * proguardFiles getDefaultProguardFile('proguard-android-optimized.txt'), 'proguard-project-market.txt'
     */

    buildTypes {

        debug {
            manifestPlaceholders = [ AdjustEnvironment:"sandbox" ]
            debuggable true
        }

        live {
            manifestPlaceholders = [ AdjustEnvironment:"sandbox" ]
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-project-live.txt'
        }


        release {
            manifestPlaceholders = [ AdjustEnvironment:"production" ]
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-project-market.txt'

        }
    }


    flavorDimensions "operatingsystem", "venture"


    productFlavors {

        android {
            flavorDimension "operatingsystem"
        }

        blackberry {
            flavorDimension "operatingsystem"
        }

        jumia {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseJumiaSigning
            resConfigs "en", "fr", "pt"
        }

        shop {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseShopSigning
            versionName "1.2"
            resConfigs "en"
        }

        daraz {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseDarazSigning
            versionName "1.2"
            resConfigs "en"
        }

        bamilo {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseBamiloSigning
            versionName "1.4"
            resConfigs "en"
        }

        jumiaMTNNg {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseJumiaMTNSigning
            versionName "2.4"
            resConfigs "en", "fr", "pt"
        }

        jumiaNokiaX {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseJumiaNokiaXSigning
            versionName "2.4"
            resConfigs "en", "fr", "pt"
        }

        jumiaSamsung {
            flavorDimension "venture"
            signingConfig signingConfigs.releaseJumiaSigning
            versionName "2.4"
            resConfigs "en", "fr", "pt"
        }

    }

    lintOptions {
        disable 'NewApi', 'RtlSymmetry', 'IconLocation', 'IconDipSize', 'IconXmlAndPng', 'RtlHardcoded'
        abortOnError false
    }

}

dependencies {
    /** LOCAL LIBS **/
    compile project(':libraries:com.mobile.framework')

    compile fileTree(dir: 'libs', include: ['*.jar'])

}


/** SET CUSTOM APPLICATION IDS DEPENDING ON FLAVOURS AND BUILD TYPE **/
android.applicationVariants.all { variant ->
    println "****************************"
    println "variant: ${variant.name}"
    println "flavor: ${variant.flavorName}"
    println "buildType: ${variant.buildType.name}"
    println "applicationId: ${variant.applicationId}"
    println "productFlavors[0]: ${variant.productFlavors[0].name}"
    println "productFlavors[1]: ${variant.productFlavors[1].name}"
    println "****************************"


    /**
     * Set application id with "com." + venture + "blackberry" or "android"(when not in debug build type)  + "dev" when in debug build type
     */
    def String venture = variant.productFlavors[1].name
    def boolean isInstall = false;

    venture = venture.replace("jumiaMTNNg", "jumia")
    venture = venture.replace("jumiaNokiaX", "jumia")
    venture = venture.replace("jumiaSamsung", "jumia")
    if (variant.productFlavors[0].name == "android") {
        variant.mergedFlavor.applicationId = "com." + venture + "." + variant.productFlavors[0].name
        println "application ID: ${variant.mergedFlavor.applicationId}"
        //e.g. com.jumia.android

        def String install = "";
        if(variant.productFlavors[1].name == "jumiaMTNNg" ){
            install = "mtnng";
            isInstall = true;
        } else if (variant.productFlavors[1].name == "jumiaNokiaX"){
            install = "nokiax";
            isInstall = true;
        }
        if(isInstall){
            variant.mergedFlavor.applicationId = "com." + venture + "." + install
            //e.g. com.jumia.nokiax
            //e.g. com.jumia.mtnng
            println "application ID: ${variant.mergedFlavor.applicationId}"
        }



        if (!isInstall && variant.buildType.name == android.buildTypes.debug.name) {
            variant.mergedFlavor.applicationId = variant.mergedFlavor.applicationId + ".dev"
            println "application ID: ${variant.mergedFlavor.applicationId}"
            //e.g. com.jumia.android.dev
        } else if(!isInstall && variant.buildType.name == android.buildTypes.live.name){
            variant.mergedFlavor.applicationId = variant.mergedFlavor.applicationId + ".live"
            println "application ID: ${variant.mergedFlavor.applicationId}"
            //e.g. com.jumia.android.live
        }
    } else {
        variant.mergedFlavor.applicationId = "com." + venture + "." + variant.productFlavors[0].name
        println "application ID: ${variant.mergedFlavor.applicationId}"
        //e.g. com.jumia.blackberry
    }

}


/**
 *  PMD static analysis
 */
task pmd(type: Pmd) {
    ignoreFailures = true
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    //ruleSets = ["java-basic", "java-strings", "java-braces"]
    ruleSetFiles = files("pmd-ruleset.xml")
    ruleSets = []

}

/**
 * Findbugs static analysis
 */
task findbugs(type: FindBugs) {
    ignoreFailures = true
    classes = fileTree('build/intermediates/classes/')
    source = fileTree('src/main/java/')
    classpath = files()
    effort = 'max'
//    shows all warnings: low, medium, high. By default is set to medium and high
//    reportLevel = "low"
    excludeFilter = file("findbugs-exclude.xml")
    reports {
        xml {
            destination "build/reports/findbugs/findbugs.xml"
        }
    }
}